group = 'net.yadaframework'
version = '0.0.1'

apply plugin: 'java-library'
apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

repositories {
    jcenter()
	mavenLocal()
}

dependencies {
		
	implementation project(':YadaWeb'), project(':YadaWebSecurity'), 
		'org.hibernate:hibernate-entitymanager:5.2.9.Final',
		'org.springframework.data:spring-data-jpa:1.11.1.RELEASE'
		
	testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebCMS",
                   "Implementation-Version": version)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebcms.properties", token: '@YADA_VERSION@', value: version)
	}
}

apply plugin: 'maven'
uploadArchives {
	dependsOn build
	repositories {
		mavenLocal()
	}
}

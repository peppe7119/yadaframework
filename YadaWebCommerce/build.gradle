plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'net.yadaframework'
version = '0.7.0'

apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
	mavenLocal()
}

dependencies {
		
	implementation project(':YadaWeb'), project(':YadaWebSecurity'), project(':YadaWebCMS'),
		'org.hibernate:hibernate-entitymanager:5.4.12.Final'
		
	testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebCommerce",
                   "Implementation-Version": archiveVersion)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebcommerce.properties", token: '@YADA_VERSION@', value: version)
	}
}

// PublishToMavenLocal task
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'yadawebcommerce'
            from components.java
        }
    }
}


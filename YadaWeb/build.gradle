group = 'net.yadaframework'
version = '0.0.7'

buildscript {
	repositories {
		mavenLocal()
	}
	dependencies {
		// Needed for test schema creation
		classpath 'net.yadaframework:YadaTools:+'
	}                    
}

apply plugin: 'java-library'
apply plugin: 'eclipse-wtp'

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

repositories {
    jcenter()
	mavenLocal()
}

dependencies {
	api 'org.apache.commons:commons-configuration2:2.1.1'
	
    implementation 'org.springframework:spring-webmvc:4.3.7.RELEASE',
		'org.springframework:spring-context-support:4.3.7.RELEASE',
		'org.springframework.data:spring-data-jpa:1.11.1.RELEASE',
		'org.springframework.social:spring-social-facebook:2.0.3.RELEASE',
		'org.hibernate:hibernate-entitymanager:5.2.9.Final',
		'org.hibernate:hibernate-validator:5.4.1.Final',
		'org.thymeleaf:thymeleaf-spring4:3.0.3.RELEASE',
		'mysql:mysql-connector-java:5.1.41',
		'commons-collections:commons-collections:3.2.2',
		'org.apache.tomcat:tomcat-servlet-api:8.0.43',
		'commons-fileupload:commons-fileupload:1.3.2',
		'org.apache.commons:commons-exec:1.3',
		'com.sun.mail:javax.mail:1.5.6',
		'com.google.guava:guava:22.0',
		'org.jsoup:jsoup:1.9.1',
		'com.fasterxml.jackson.core:jackson-annotations:2.9.+',
		'com.fasterxml.jackson.core:jackson-core:2.9.+',
		'com.fasterxml.jackson.core:jackson-databind:2.9.+',
		'org.seleniumhq.selenium:selenium-support:2.53.1',
		'org.littleshoot:littleproxy:1.1.2',
		'org.quartz-scheduler:quartz:2.2.3', // TODO remove
		'org.flywaydb:flyway-core:4.2.0'
	
	// Needed to run in Tomcat
	// runtime 'commons-beanutils:commons-beanutils:1.9.3'
	// runtime 'commons-jxpath:commons-jxpath:1.3'

	testImplementation 'junit:junit:4.12',
		'org.springframework:spring-test:4.3.7.RELEASE'
	
	testRuntime 'commons-beanutils:commons-beanutils:1.9.3',
		'commons-jxpath:commons-jxpath:1.3'

		
}

task listJars {
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWeb",
                   "Implementation-Version": version)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadaweb.properties", token: '@YADA_VERSION@', value: version)
	}
}


apply plugin: 'maven'

// http://stackoverflow.com/a/27853822/587641
// TODO: Il javadoc genera errori che devono essere sistemati prima di poterlo usare
/*
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}   

artifacts {
    archives javadocJar
}
*/

// Per usare meglio maven vedi qui: http://stackoverflow.com/questions/24751996/gradle-configuration-to-download-custom-plugin-dependencies

uploadArchives {
	dependsOn build
	repositories {
		mavenLocal()
	}
}

configurations {
	hibtools {
		extendsFrom configurations.implementation
	}
}

dependencies {
	hibtools files("$buildDir/classes/main")
	hibtools files("$buildDir/classes/test") // Needed for yadaPersistenceUnit
	hibtools 'org.hibernate:hibernate-tools:5.+'
}

task testDbSchema(dependsOn: [testClasses], type: net.yadaframework.tools.YadaCreateDbSchemaTask) {
	inputs.files configurations.hibtools
	outputfilename = "yadatest.sql"
	doFirst {
		new File("$buildDir/classes/test/META-INF").mkdirs()
		copy {
			from "$buildDir/resources/test/META-INF/persistence.test.xml"
			into "$buildDir/classes/test/META-INF"
			rename 'persistence.test.xml', 'persistence.xml'
		} 
	}
	doLast { 
		delete "$buildDir/classes/test/META-INF/persistence.xml"
	}
}



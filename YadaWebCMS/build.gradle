plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id 'eclipse-wtp'
}

group = 'net.yadaframework'
version = '0.4.1.2'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}	
	withSourcesJar()
    withJavadocJar() // https://stackoverflow.com/a/75710366/587641
}

eclipse {
	jdt {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		// https://stackoverflow.com/a/35302104/587641
		file {
      		File dir = file('.settings')
      		dir.mkdirs()
      		File f = file('.settings/org.eclipse.core.resources.prefs')
      		if (!f.exists()) {
      			f.write('eclipse.preferences.version=1\n')
      			f.append('encoding/<project>=utf-8')
      		}
    	}
	}
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
		
	implementation project(':YadaWeb'), project(':YadaWebSecurity'), 
		'org.springframework:spring-webmvc:4.3.7.RELEASE',
		'org.hibernate:hibernate-entitymanager:5.2.9.Final',
		'org.springframework.data:spring-data-jpa:1.11.1.RELEASE',
		'com.fasterxml.jackson.core:jackson-annotations:2.13.4',
		'com.fasterxml.jackson.core:jackson-databind:2.13.4',
		'com.fasterxml.jackson.core:jackson-core:2.13.4',
		'org.apache.commons:commons-io:1.3.2'
		
		
	testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Title": "YadaWebCMS",
                   "Implementation-Version": archiveVersion)
    }
}

// Set the yada version in the properties file
processResources {
	doLast {
		ant.replace(file: "${sourceSets.main.output.resourcesDir}/net.yadaframework.yadawebcms.properties", token: '@YADA_VERSION@', value: version)
	}
}

javadoc {
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
    failOnError = false
}


